// *****************************
// ***** UNIMARKUP GENERAL *****
// *****************************

// ***** SPACING *****
um_newline = _{ NEWLINE }
um_nbsp = _{ SPACE_SEPARATOR }
blank_line = @{ um_newline ~ um_nbsp* ~ um_newline+ }

// ***** UNIMARKUP *****
unimarkup_block = _{ enclosed_block | atomic_block }
unimarkup = { SOI ~ (unimarkup_block ~ (blank_line | EOI))+ }


// *****************************
// ****** ENCLOSED BLOCKS ******
// *****************************

// ****** ENCLOSED BLOCK DELIMITERS ******
verbatim_delimiter = @{ !"~~~[" ~ "~"{3,} }
renderer_delimiter = @{ !"'''[" ~ "'"{3,} }
preamble_delimiter = @{ ";;;" }

// ****** ENCLOSED BLOCK ******
delimiter = _{ verbatim_delimiter | renderer_delimiter | preamble_delimiter }
enclosed_body = { (!(um_newline ~PEEK) ~ ANY)+ }
enclosed_end = @{ um_newline ~ POP }
enclosed_block = { PUSH(delimiter) ~ enclosed_body ~ enclosed_end }


// ***************************
// ****** ATOMIC BLOCKS ******
// ***************************

// ****** ATOMIC BLOCK ******
atomic_block = { (!blank_line ~ ANY)+ }

// ****** HEADINGS ******
heading_start = @{ um_newline? ~ "#"{1,6} ~ um_nbsp }
heading_content = { ( !( um_newline ~ heading_start ) ~ ANY )+ ~ um_newline? }
heading = { heading_start ~ heading_content }
headings = { heading+ }

// ****** PARAGRAPH ******
paragraph = { ANY+ }

// ****** INLINE FORMATTING ******
text = { (!(inline_delim) ~ ANY)+ }
inline_format = { (inline_block | text)+ }
inline_block = _{ bold | italic | subscript | superscript | verbatim }
inline_body = _{ (!(PEEK) ~ (inline_block | text))+ }

// ****** INLINE FORMATTING STYLES ******
bold_delim = _{ "**" }
italic_delim = _{ "*" }
subscript_delim = _{ "_" }
superscript_delim = _{ "^" }
verbatim_delim = _{ "`" }
inline_delim = { bold_delim | italic_delim | subscript_delim | superscript_delim | verbatim_delim }

bold = { PUSH(bold_delim) ~ inline_body ~ POP }
italic = { PUSH(italic_delim) ~ inline_body ~ POP }
subscript = { PUSH(subscript_delim) ~ inline_body ~ POP }
superscript = { PUSH(superscript_delim) ~ inline_body ~ POP }
verbatim = { PUSH(verbatim_delim) ~ inline_body ~ POP }