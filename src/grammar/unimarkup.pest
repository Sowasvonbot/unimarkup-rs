// *****************************
// ***** UNIMARKUP GENERAL *****
// *****************************

// ***** SPACING *****
um_newline = _{ NEWLINE }
um_nbsp = _{ SPACE_SEPARATOR }
blank_line = @{ um_newline ~ um_nbsp* ~ um_newline+ }

// ***** UNIMARKUP *****
unimarkup_block = _{ enclosed_block | atomic_block }
unimarkup = { SOI ~ (unimarkup_block ~ (blank_line | EOI))+ }


// *****************************
// ****** ENCLOSED BLOCKS ******
// *****************************

// ****** ENCLOSED BLOCK DELIMITERS ******
verbatim_delimiter = @{ !"~~~[" ~ "~"{3,} }
renderer_delimiter = @{ !"'''[" ~ "'"{3,} }
preamble_delimiter = @{ ";;;" }

// ****** ENCLOSED BLOCK ******
delimiter = _{ verbatim_delimiter | renderer_delimiter | preamble_delimiter }
enclosed_body = { (!(um_newline ~PEEK) ~ ANY)+ }
enclosed_end = @{ um_newline ~ POP }
enclosed_block = { PUSH(delimiter) ~ enclosed_body ~ enclosed_end }


// ***************************
// ****** ATOMIC BLOCKS ******
// ***************************

// ****** ATOMIC BLOCK ******
atomic_block = { (!blank_line ~ ANY)+ }

// ****** HEADINGS ******
heading_start = @{ um_newline? ~ "#"{1,6} ~ um_nbsp }
heading_content = { ( !( um_newline ~ heading_start ) ~ ANY )+ ~ um_newline? }
heading = { heading_start ~ heading_content }
headings = { heading+ }

// ****** PARAGRAPH ******
paragraph = { ANY+ }

// ****** VERBATIM ******
verbatim_lang    = { ( !( PEEK | ( um_nbsp* ~ um_newline ) ) ~ ANY )+ }
verbatim_content = { ( !verbatim_end ~ ANY)+ }
verbatim_end     = @{ NEWLINE ~ PEEK }
verbatim         = { PUSH(verbatim_delimiter) ~ verbatim_lang? ~ (SPACE_SEPARATOR* ~ NEWLINE) ~ verbatim_content ~ verbatim_end }
