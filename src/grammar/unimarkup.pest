blank_line = @{ NEWLINE ~ SPACE_SEPARATOR* ~ NEWLINE }

// --------- ATOMIC BLOCKS --------- \\ 
atomic_block = { (!(NEWLINE ~ SPACE_SEPARATOR* ~ NEWLINE) ~ ANY)+ }

// --------- NESTED BLOCK DELIMITER --------- \\ 
verbatim_delimiter = { !"~~~[" ~ "~"{3,} }
renderer_delimiter = { !"'''[" ~ "'"{3,} }
preamble_delimiter = { ";;;" }

// --------- NESTED BLOCKS --------- \\ 
delimiter = _{ verbatim_delimiter | renderer_delimiter | preamble_delimiter }
nested_body = { (!PEEK ~ ANY)+ }
nested_block = { PUSH(delimiter) ~ nested_body ~ POP }

unimarkup_block = _{ nested_block | atomic_block }

unimarkup = { SOI ~ (unimarkup_block ~ (blank_line | EOI))+ }

// ****************************
// ********* BLOCKS ***********
// ****************************

// ******** HEADING ******** \\

heading_start = @{ NEWLINE? ~ "#"{1,6} ~ SPACE_SEPARATOR }
heading_content = { ( !( NEWLINE ~ heading_start ) ~ ANY )+ ~ NEWLINE? }
heading = { heading_start ~ heading_content }
headings = { heading+ }

// ******** PARAGRAPH ******** \\

paragraph_content = { ((LETTER)+ ~ SPACE_SEPARATOR*)+ }
paragraph = { (!( (NEWLINE | SOI) ~ heading_start) ~  paragraph_content )+ }
paragraphs = { paragraph+ }