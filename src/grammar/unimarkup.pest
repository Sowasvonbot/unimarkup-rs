// *****************************
// ***** UNIMARKUP GENERAL *****
// *****************************

// ***** SPACING *****
um_newline = _{ NEWLINE }
um_nbsp = _{ SPACE_SEPARATOR }
blank_line = @{ um_newline ~ um_nbsp* ~ um_newline+ }

// ***** UNIMARKUP *****
unimarkup_block = _{ enclosed_block | atomic_block }
unimarkup = { SOI ~ (unimarkup_block ~ (blank_line | EOI))+ }


// *****************************
// ****** ENCLOSED BLOCKS ******
// *****************************

// ****** ENCLOSED BLOCK DELIMITERS ******
verbatim_delimiter = @{ !"~~~[" ~ "~"{3,} }
renderer_delimiter = @{ !"'''[" ~ "'"{3,} }
preamble_delimiter = @{ ";;;" }

// ****** ENCLOSED BLOCK ******
delimiter = _{ verbatim_delimiter | renderer_delimiter | preamble_delimiter }
enclosed_body = { (!(um_newline ~PEEK) ~ ANY)+ }
enclosed_end = @{ um_newline ~ POP }
enclosed_block = { PUSH(delimiter) ~ enclosed_body ~ enclosed_end }


// ***************************
// ****** ATOMIC BLOCKS ******
// ***************************

// ****** ATOMIC BLOCK ******
atomic_block = { (!blank_line ~ ANY)+ }

// ****** HEADINGS ******
heading_start = @{ um_newline? ~ "#"{1,6} ~ um_nbsp }
heading_content = { ( !( um_newline ~ ( heading_start | ( um_nbsp* ~ attributes ) ) ) ~ ANY )+ }
heading = { heading_start ~ heading_content ~ um_nbsp* ~ ( um_newline ~ um_nbsp* ~ attributes )? }
headings = { heading+ }

// ****** PARAGRAPH ******
par_attr  = _{ um_newline ~ um_nbsp* ~ attributes }
par_content = { (!par_attr ~ ANY )+ }
paragraph = {  par_content  ~ par_attr? }

// ****** INLINE FORMATTING ******
escaped = { "\\" }
escaped_delim = @{ escaped ~ (italic_delim | subscript_delim | superscript_delim | verbatim_delim)}
plain = @{ ((escaped_delim) | (!(inline_delim) ~ ANY))+ }
inline_format = { (inline_block | plain)+ }
inline_block = _{ bold_inline | italic_inline | subscript_inline | superscript_inline | verbatim_inline }
inline_body = _{ (!(PEEK) ~ (inline_block | plain))+ }

// ****** INLINE FORMATTING STYLES ******
bold_delim = _{ "**" }
italic_delim = _{ "*" }
subscript_delim = _{ "_" }
superscript_delim = _{ "^" }
verbatim_delim = _{ "`" }
inline_delim = { bold_delim | italic_delim | subscript_delim | superscript_delim | verbatim_delim }

bold_inline = { PUSH(bold_delim) ~ inline_body ~ POP }
italic_inline = { PUSH(italic_delim) ~ inline_body ~ POP }
subscript_inline = { PUSH(subscript_delim) ~ inline_body ~ POP }
superscript_inline = { PUSH(superscript_delim) ~ inline_body ~ POP }
verbatim_inline = { PUSH(verbatim_delim) ~ inline_body ~ POP }

// ****** VERBATIM ******
verbatim_lang    = { ( !( PEEK | ( um_nbsp* ~ um_newline ) ) ~ ANY )+ }
verbatim_content = { ( !verbatim_end ~ ANY)+ }
verbatim_end     = @{ um_newline ~ PEEK }
verbatim_attrs   = _{ um_nbsp* ~ attributes }
verbatim         = { PUSH(verbatim_delimiter) ~ ( verbatim_attrs | verbatim_lang )? ~ ( um_nbsp* ~ um_newline ) ~ verbatim_content ~ verbatim_end }

// ****** ATTRIBUTES ******
// Note: Actual parsing is done in code using serde_json
attr_body = { ( ( !( "{" | "}" ) ~ ( ANY ) ) | attributes )+ }
attributes = @{ "{" ~ attr_body ~ "}" }